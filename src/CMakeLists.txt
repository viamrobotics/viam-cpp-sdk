# Copyright 2023 Viam Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

set(PROTO_GEN_DIR ${CMAKE_CURRENT_SOURCE_DIR})

if (VIAMCPPSDK_USE_DYNAMIC_PROTOS)

  # Look for the `buf` command in the usual places, and use it if
  # found. If we can't find it, try to download it and use that.
  #
  # TODO: File an upstream issue with `buf.build` to add
  # `find_package` support for it, then use it.
  #
  find_program(BUF_COMMAND buf)
  if (NOT BUF_COMMAND)
    file(
      DOWNLOAD
      https://github.com/bufbuild/buf/releases/latest/download/buf-${CMAKE_HOST_SYSTEM_NAME}-${CMAKE_HOST_SYSTEM_PROCESSOR}
      ${CMAKE_CURRENT_BINARY_DIR}/buf_latest
      STATUS buf_status
    )
    list(GET buf_status 0 buf_status_code)
    list(GET buf_status 1 buf_status_string)

    if(NOT buf_status_code EQUAL 0)
      message(FATAL_ERROR "No local `buf` program found (try setting PATH?) and failed to download: ${buf_status_string}")
    endif()

    set(BUF_COMMAND ${CMAKE_CURRENT_BINARY_DIR}/buf_latest)
    file(CHMOD ${BUF_COMMAND} PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE)
  endif()

  set(PROTO_GEN_DIR ${CMAKE_CURRENT_BINARY_DIR})

  # Buf only goes back to 1.41.1 for generation against their "custom
  # remote" registry (see https://buf.build/grpc/plugins/cpp), and we
  # need a differently shaped file to do local generation if we must
  # handle versions older than that. For versions newer than 1.51,
  # "remote plugins" seem to be the thing to use for now.
  #
  # TODO: Should we have an option to always do local generation so
  # offline development always works?
  #
  if ((NOT VIAMCPPSDK_OFFLINE_PROTO_GENERATION) AND (VIAMCPPSDK_GRPCXX_VERSION VERSION_GREATER 1.51.1))
    configure_file(
      gen/etc/buf.gen.remote.plugin.yaml.in
      buf.gen.yaml
    )
  elseif ((NOT VIAMCPPSDK_OFFLINE_PROTO_GENERATION) AND (VIAMCPPSDK_GRPCXX_VERSION VERSION_GREATER_EQUAL 1.41.1))
    configure_file(
      gen/etc/buf.gen.custom.remote.yaml.in
      buf.gen.yaml
    )
  else()
    configure_file(
      gen/etc/buf.gen.local.yaml.in
      buf.gen.yaml
    )
  endif()

  add_custom_command(
    OUTPUT
    # Unfortunately, there isn't a good way to know in advance what
    # files will be generated by invoking `buf generate`.  Instead, we
    # just list the files here that we know we need in the
    # `add_library` call below. But we will find in practice that more
    # files are generated.
    #
    # TODO: We reiterate this list below in `add_library`. Can we DRY
    # it sanely? We would needd to filter out (or add in) the headers.

    # This list is needed for the core library.
    ${PROTO_GEN_DIR}/gen/common/v1/common.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/common/v1/common.grpc.pb.h
    ${PROTO_GEN_DIR}/gen/common/v1/common.pb.cc
    ${PROTO_GEN_DIR}/gen/common/v1/common.pb.h
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.grpc.pb.h
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.pb.cc
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.pb.h
    ${PROTO_GEN_DIR}/gen/google/api/annotations.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/annotations.pb.h
    ${PROTO_GEN_DIR}/gen/google/api/httpbody.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/httpbody.pb.h
    ${PROTO_GEN_DIR}/gen/google/api/http.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/http.pb.h
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.grpc.pb.h
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.pb.cc
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.pb.h

    # These are needed for `echo` example, which is awkward. These can
    # probably be removed if that example is pulled.
    ${PROTO_GEN_DIR}/gen/proto/rpc/examples/echo/v1/echo.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/proto/rpc/examples/echo/v1/echo.grpc.pb.h
    ${PROTO_GEN_DIR}/gen/proto/rpc/examples/echo/v1/echo.pb.cc
    ${PROTO_GEN_DIR}/gen/proto/rpc/examples/echo/v1/echo.pb.h

    COMMAND ${BUF_COMMAND} generate buf.build/viamrobotics/goutils --template buf.gen.yaml
    COMMAND ${BUF_COMMAND} generate buf.build/googleapis/googleapis --template buf.gen.yaml --path google/rpc --path google/api
    COMMAND ${BUF_COMMAND} generate buf.build/viamrobotics/api --template buf.gen.yaml --path common,component,robot,service,module
    MAIN_DEPENDENCY buf.gen.yaml
  )

  add_custom_target(
    generate-dynamic-protos
    # This must be one of the files listed in `add_custom_command` above,
    # but it doesn't matter which one, we just need to have a dependency edge
    # into the files produced by that command.
    #
    # TODO: If we DRY as suggested above, this could just be the first list item.
    #
    DEPENDS ${PROTO_GEN_DIR}/gen/common/v1/common.grpc.pb.cc
  )

  add_custom_target(
    update-static-protos
    DEPENDS generate-dynamic-protos
  )

  add_custom_command(
    TARGET update-static-protos
    COMMAND ${CMAKE_COMMAND} -E copy_directory ${PROTO_GEN_DIR}/gen ${CMAKE_SOURCE_DIR}/src/gen
  )
endif(VIAMCPPSDK_USE_DYNAMIC_PROTOS)

# I dislike exporting values via `CACHE` but for now we need this
# state over in the `examples` directory. Hopefully when we do some
# physical refactoring that can be eliminated.
set(PROTO_GEN_DIR ${PROTO_GEN_DIR} CACHE INTERNAL "")


# Declare the viamcpp library, and attach sources,
# headers for installation
add_library(viamcpp)

# TODO: This is analogized from
# https://gitlab.kitware.com/cmake/cmake/-/tree/master/Help/guide/importing-exporting/MathFunctionsComponents
# but it doesn't really seem to do anything. Everything works fine if
# it is removed, and naming the alias something other than `viamcpp`
# doesn't make it available under that alias anyway, as one might
# hope. What is it supposed to do?
add_library(viam-cpp-sdk::viamcpp ALIAS viamcpp)

target_sources(viamcpp
  PRIVATE
    ${PROTO_GEN_DIR}/gen/common/v1/common.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/common/v1/common.pb.cc
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.grpc.pb.cc
    ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/annotations.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/http.pb.cc
    ${PROTO_GEN_DIR}/gen/google/api/httpbody.pb.cc
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.pb.cc
    ${PROTO_GEN_DIR}/gen/robot/v1/robot.grpc.pb.cc
    common/proto_type.cpp
    common/utils.cpp
    components/component_base.cpp
    components/resource_manager.cpp
    registry/registry.cpp
    robot/client.cpp
    rpc/dial.cpp
  PUBLIC FILE_SET viamcpp_public_includes TYPE HEADERS
    FILES
      common/proto_type.hpp
      common/utils.hpp
      components/component_base.hpp
      components/resource_manager.hpp
      components/service_base.hpp
      registry/registry.hpp
      robot/client.hpp
      robot/service.hpp
      rpc/dial.hpp
  PUBLIC FILE_SET viamcpp_public_pb_includes TYPE HEADERS
    BASE_DIRS
      ${PROTO_GEN_DIR}/gen
    FILES
      ${PROTO_GEN_DIR}/gen/google/api/annotations.pb.h
      ${PROTO_GEN_DIR}/gen/google/api/http.pb.h
      ${PROTO_GEN_DIR}/gen/google/api/httpbody.pb.h
      ${PROTO_GEN_DIR}/gen/common/v1/common.pb.h
      ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.grpc.pb.h
      ${PROTO_GEN_DIR}/gen/component/camera/v1/camera.pb.h
      ${PROTO_GEN_DIR}/gen/robot/v1/robot.grpc.pb.h
      ${PROTO_GEN_DIR}/gen/robot/v1/robot.pb.h
)

target_include_directories(viamcpp
  PUBLIC
    "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>"
    "$<BUILD_INTERFACE:${PROTO_GEN_DIR}/gen>"
    "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"

    # These expand to nothing unless we picked up grpc/proto via
    # pkgconfig rather than find_package
    "$<BUILD_INTERFACE:${VIAMCPPSDK_GRPCXX_INCLUDE_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_GRPCXX_INCLUDE_DIRS}>"
    "$<BUILD_INTERFACE:${VIAMCPPSDK_GRPC_INCLUDE_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_GRPC_INCLUDE_DIRS}>"
    "$<BUILD_INTERFACE:${VIAMCPPSDK_PROTOBUF_INCLUDE_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_PROTOBUF_INCLUDE_DIRS}>" )

# This shouldn't be necessary, but we need it so that the installation
# path of the C++ SDK is added to the link line of consumers to that
# `-lviam_rust_utils` can be meaningfully resolved.
#
# TODO: Remove this when we can import `viam_rust_utils` as a real
# imported target with its own properties.
target_link_directories(viamcpp
  INTERFACE
    "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>"
    "$<INSTALL_INTERFACE:${CMAKE_INSTALL_LIBDIR}>"

    # These expand to nothing unless we picked up grpc/proto via
    # pkg-config rather than find_package
    "$<BUILD_INTERFACE:${VIAMCPPSDK_GRPCXX_LIBRARY_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_GRPCXX_LIBRARY_DIRS}>"
    "$<BUILD_INTERFACE:${VIAMCPPSDK_GRPC_LIBRARY_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_GRPC_LIBRARY_DIRS}>"
    "$<BUILD_INTERFACE:${VIAMCPPSDK_PROTOBUF_LIBRARY_DIRS}>"
    "$<INSTALL_INTERFACE:${VIAMCPPSDK_PROTOBUF_LIBRARY_DIRS}>"
)

target_link_libraries(viamcpp
  PUBLIC ${VIAMCPPSDK_GRPCXX_LINK_LIBRARIES}
  PUBLIC ${VIAMCPPSDK_GRPC_LINK_LIBRARIES}
  PUBLIC ${VIAMCPPSDK_PROTOBUF_LINK_LIBRARIES}
  PUBLIC Boost::headers
  PRIVATE Boost::log
  PRIVATE viam_rust_utils
)

install(
  TARGETS viamcpp
  EXPORT LibTargets
  LIBRARY COMPONENT runtime
  FILE_SET viamcpp_public_includes COMPONENT dev
  FILE_SET viamcpp_public_pb_includes COMPONENT dev
)

install(
  EXPORT LibTargets
  FILE viam-cpp-sdkLibTargets.cmake
  NAMESPACE viam-cpp-sdk::
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/viam-cpp-sdk
)


# Generate a `pkg-config` file for the library component of the
# project. This is realy best effort as we can't necessarily capture
# the full set of requirements and dependencies.
#
# TODO: We should be pulling the paths and libaries out as properties
# and interpolating them in, but I can't yet figure out the right
# incantation to get CMake to expand the various generator expressions
# in the "INSTALL" context.

configure_file(
  config/viam-cpp-sdk-libviamcpp.pc.in
  config/viam-cpp-sdk-libviamcpp.pc
  @ONLY
)

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/config/viam-cpp-sdk-libviamcpp.pc
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig
  COMPONENT dev
)
